apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

sourceCompatibility = java_version
targetCompatibility = java_version

dependencies {
	compile "com.google.guava:guava:20.0"
	compile "org.apache.commons:commons-lang3:3.5"
	testCompile "junit:junit:4.12"
}

archivesBaseName = 'EXComms'
version = "${version_major}.${version_minor}.${version_patch}"
group = 'code.elix_x.excomms'
def classifier = null
if(version_channel != 'release') classifier = version_channel

jar.classifier = classifier

task generatePom {
    doLast {
		def pomFile = "$buildDir/libs/$archivesBaseName-$version${classifier != null ? '-' + classifier : ''}.pom"
        pom {
            project {
                name 'EXComms'
				description 'Elix\'x Common Library for Java 8+'
				url 'https://github.com/Elix-x/EXComms'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
				developers {
					developer {
						name 'Elix X'
						email 'elix.x.ggl@gmail.com'
						url 'https://github.com/Elix-x'
					}
				}
				scm {
					connection 'scm:git:git://github.com/Elix-x/EXComms.git'
					developerConnection 'scm:git:ssh://github.com:Elix-x/EXComms.git'
					url 'https://github.com/Elix-x/EXComms/tree/master'
				}
            }
        }.writeTo(pomFile)
		ant.checksum file: pomFile
		ant.checksum(file: pomFile, algorithm: 'SHA')
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}
sourceJar.classifier = "$classifier-sources"

task javadocJar(type: Jar, dependsOn: 'javadoc'){
	from "$buildDir/docs/javadoc"
}
javadocJar.classifier = "$classifier-javadoc"

tasks.withType(Jar) { task ->
    task.doLast {
		ant.checksum file: task.archivePath
		ant.checksum(file: task.archivePath, algorithm: 'SHA')
	}
}

task postBuild(dependsOn: ['sourceJar', 'javadocJar', 'generatePom']){

}

assemble.finalizedBy postBuild